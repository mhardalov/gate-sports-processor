Phase: Location
Input: Token Entity Location Person WinnerRelation
Options: control = appelt

Rule: SetOfEntitiesAnnotation
(
	(
		(
	 		(
				{Person, !Location}|
				{Entity, !Location}
			)
		)
		({Location} | {Person} | {Entity})*
	):entry
	(
		({Token.string == "("})[1]
		(({Token})[0,2]):location
		({Token.string == ")"})[1]
	)*
):match
-->
:entry.Sample = {rule = "test"},
{
	gate.AnnotationSet match = (gate.AnnotationSet)bindings.get("match");
	gate.AnnotationSet location = (gate.AnnotationSet)bindings.get("location");
	gate.AnnotationSet entity = (gate.AnnotationSet)bindings.get("entry");

	String annotationType;
	gate.FeatureMap features = Factory.newFeatureMap();
	
	

	HashSet fNames = new HashSet();
	fNames.add("gender");
	gate.AnnotationSet persons = entity.get("Person", fNames);

	if(persons != null && persons.size() > 0) {
		annotationType = "person";
	} else {
		annotationType = "team";
	}

	features.put("rule","SetOfEntitiesAnnotation");
	features.put("type", annotationType);
	if (location != null && location.size() > 0) {
		String locStr = doc.getContent().toString().substring(
			location.firstNode().getOffset().intValue(),
			location.lastNode().getOffset().intValue());

		features.put("location", locStr);
	}

	String entityStr = doc.getContent().toString().substring(
			entity.firstNode().getOffset().intValue(),
			entity.lastNode().getOffset().intValue());

	features.put("entity", entityStr);
	outputAS.add(match.firstNode(), match.lastNode(), "NEAnnotations", features);
}

Rule: LocationAsTeam
(
	({WinnerRelation})
	({Location}):match
)
-->
:match.NEAnnotations = {rule= "LocationAsTeam" }
